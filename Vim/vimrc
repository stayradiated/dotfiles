
" ~/.vimrc
"

"
" Compatability {{{
" -----------------------------------------------------------------------------

set nocompatible         " use vim defaults instead of vi
set encoding=utf-8       " always encode in utf

" }}}
"
" Load Plugins {{{
" -----------------------------------------------------------------------------

if has('vim_starting')
  set runtimepath+=~/.vim/bundle/neobundle.vim
endif

call neobundle#rc(expand('~/.vim/bundle/'))

" NeoBundle
NeoBundleFetch 'Shougo/neobundle.vim'

" VimProc
NeoBundle 'Shougo/vimproc.vim', {
  \ 'build' : {
  \   'mac' : 'make -f make_mac.mak',
  \   'unix': 'make -f make_unix.mak'
  \   }
  \ }

" Themes
NeoBundle 'itchyny/lightline.vim'
NeoBundle 'w0ng/vim-github-theme'
NeoBundle 'w0ng/vim-hybrid'
NeoBundle 'altercation/vim-colors-solarized'

" Extensions
NeoBundle 'Shougo/unite.vim'
NeoBundle 'Shougo/vimfiler.vim'
NeoBundle 'Shougo/neocomplete.vim'
NeoBundle 'tpope/vim-fugitive'

" Editing
NeoBundle 'scrooloose/syntastic'
NeoBundle 'tpope/vim-surround'
NeoBundle 'tpope/vim-unimpaired'
NeoBundle 'mattn/emmet-vim'
NeoBundle 'tomtom/tcomment_vim'
NeoBundle 'godlygeek/tabular'

" Languages
NeoBundle 'kchmck/vim-coffee-script'
NeoBundle 'nelstrom/vim-markdown-folding'

" Filetype dectection
filetype plugin indent on

" Check plugins
NeoBundleCheck

"
" }}}
"
" Settings {{{
" -----------------------------------------------------------------------------

" Enable syntax highlighting
syntax on

" General
set backspace=2          " enable <BS> for everything
set colorcolumn=80       " visual indicator of column
let &colorcolumn=join(range(81,999),",")
set scrolloff=5          " minimum lines at top/bottom when scrolling
set completeopt-=preview " don't show preview window
set cursorline           " highlight the line where the cursor is
set fcs=vert:│,fold:-    " solid instead of broken line for vert splits
set hidden               " hide when switching buffers, don't unload
set laststatus=2         " always show the status line
set lazyredraw           " don't update screen when executing macros
set mouse=a              " enable mouse in all modes
set noshowmode           " clashes with airline
set linebreak            " break lines
set listchars=tab:▸\ ,trail:▫
set nolist                 " show whitespace
set wrap                 " enable wordwrap
set relativenumber       " show line numbers relative to the current line
set number               " show absolute line number on current line
set showcmd              " show command on last line of screen
set noshowmatch          " don't show bracket matches
set spelllang=en_nz      " spell check with New Zealand English
set textwidth=0          " don't break lines after some maximum width
set title                " use filename in window title
set wildmenu             " enhanced cmd line completion
set gdefault             " replace everything by default
set autochdir            " change directory to current buffer when opening files


if has('macunix')
  " CodeKit Compatibility
  set nobackup
  set nowritebackup
  set noswapfile
else
  " Backup swap files
  set backup
  set backupdir=~/.vim/tmp
  set backupskip=/tmp/*,/private/tmp/*
  set directory=~/.vim/tmp
  set writebackup
endif

" Folding
set foldignore=          " don't ignore anything when folding
set foldlevelstart=99    " no folds closed on open
set foldmethod=marker    " collapse code using markers

" Tabs
set autoindent           " copy indent from previous line
set expandtab            " replace tabs with spaces
set shiftwidth=2         " spaces for autoindenting
set smarttab             " <BS> removes shiftwidth worth of spaces
set softtabstop=2        " spaces for editing, e.g. <Tab> or <BS>
set tabstop=2            " spaces for <Tab>

" Searches
set hlsearch             " highlight search results
set incsearch            " search whilst typing
set ignorecase           " case insensitive searching
set smartcase            " override ignorecase if upper case typed

" Colours
set t_Co=256
let g:hybrid_use_Xresources = 1
set background=light
colorscheme solarized

" Fix escape key lag in terminal
set timeoutlen=1000
set ttimeoutlen=0

" gVim
if has('gui_running')

  " Hide GUI
  set guioptions-=m      " remove menu
  set guioptions-=T      " remove toolbar
  set guioptions-=r      " remove right scrollbar
  set guioptions-=b      " remove bottom scrollbar
  set guioptions-=L      " remove left scrollbar

  " Paste from PRIMARY and CLIPBOARD
  inoremap <silent> <M-v> <Esc>"+p`]a
  inoremap <silent> <S-Insert> <Esc>"*p`]a

  " Fonts
  if has('macunix')
    set guifont=Meslo\ LG\ M\ DZ:h14
  else
    set guifont=tamzen\ 14
  endif

endif

" Toggle cursor shape in iTerm2
if exists('$TMUX')
  let &t_SI = "\<Esc>Ptmux;\<Esc>\<Esc>]50;CursorShape=1\x7\<Esc>\\"
  let &t_EI = "\<Esc>Ptmux;\<Esc>\<Esc>]50;CursorShape=0\x7\<Esc>\\"
elseif &term =~ "xterm"
  let &t_SI = "\<Esc>]50;CursorShape=1\x7"
  let &t_EI = "\<Esc>]50;CursorShape=0\x7"
endif

" vimdiff
if &diff
  set diffopt=filler,foldcolumn:0
endif

" Netrw settings
let g:netrw_liststyle=3    " Use tree-mode as default
let g:netrw_browse_split=4 " Open file in previous buffer
let g:netrw_preview=1      " Show preview in a vertical split
let g:netrw_altv=1
let g:netrw_list_hide = ".git,.sass-cache,.*"

" }}}
"
" Mappings {{{
" -----------------------------------------------------------------------------

" Vim Training mode
map <Left> <Nop>
map <Right> <Nop>
map <Up> <Nop>
map <Down> <Nop>

" Scrolling
map <ScrollWheelUp> <C-Y>
map <ScrollWheelDown> <C-E>

" Map Leader
let mapleader = ","

" Remap Tab to %
nnoremap <tab> %
vnoremap <tab> %

" Toggle fold
nnoremap <space> za

" Toggle spellcheck
nnoremap <leader>s :set spell!<CR>

" Toggle hlsearch for current results
nnoremap <leader>h :nohlsearch<CR>

" Search for trailing whitespace
nnoremap <leader>w /\s\+$<CR>

" Toggle last active bufer
nnoremap <leader><Tab> :b#<CR>

" Delete a buffer without closing the window
nnoremap <leader>d :bp<bar>sp<bar>bn<bar>bd<CR>

" Switch colon and semi-colon
nnoremap ; :
nnoremap : ;
vnoremap ; :
vnoremap : ;

" Split windows
nnoremap <C-h> <C-w>h
nnoremap <C-j> <C-w>j
nnoremap <C-k> <C-w>k
nnoremap <C-l> <C-w>l

" Remember selection when indenting
vnoremap > >gv
vnoremap < <gv

" Better movement over wrapped lines
nnoremap j gj
nnoremap k gk

" Paste mode for terminals
nnoremap <F2> :set invpaste paste?<CR>
set pastetoggle=<F2>

nnoremap <leader>m :call ToggleFoldMethod()<CR>

" Shortcut for Tabularize
vnoremap <leader>t :Tabularize /

" Check file for errors
nnoremap <leader>c :SyntasticCheck<CR>

" Show Quickfix window for Syntastic errors
nnoremap <leader>E :Errors<CR>

" Unite.vim
nnoremap <leader>t :<C-u>Unite -no-split -buffer-name=files   -start-insert file_rec/async<CR>
nnoremap <leader>T :<C-u>Unite -no-split -buffer-name=files   -start-insert file_rec/async:!<CR>
nnoremap <leader>f :<C-u>Unite -no-split -buffer-name=files   -start-insert file<CR>
nnoremap <leader>r :<C-u>Unite -no-split -buffer-name=mru     -start-insert file_mru<CR>
nnoremap <leader>y :<C-u>Unite -no-split -buffer-name=yank    -start-insert history/yank<CR>
nnoremap <leader>e :<C-u>Unite -no-split -buffer-name=buffer  -start-insert buffer<CR>
nnoremap <leader>/ :<C-u>Unite -no-split -buffer-name=buffer  -start-insert grep:.<CR>

" Shortcut for Vimfiler
nnoremap <leader><space> :VimFilerExplorer<CR>
" nnoremap <leader><space> :call ToggleVExplorer()<CR>

" Keep search pattern at the center of the screen
nnoremap <silent> n nzz
nnoremap <silent> N Nzz
nnoremap <silent> * *zz
nnoremap <silent> # #zz

" }}}
"
" Plugin Settings {{{
" -----------------------------------------------------------------------------

" Lightline
let g:lightline = {
  \ 'colorscheme': 'solarized'
  \ }

" VimFiler
let g:vimfiler_as_default_explorer = 1
let g:vimfiler_enable_auto_cd = 1

" Unite
let g:unite_source_history_yank_enable = 1
let g:unite_source_file_rec_ignore_pattern = '\%(node_modules\/\|.sass-cache\/\|.git\/\)'

" Let's use ag
let g:unite_source_grep_command = 'ag'
let g:unite_source_grep_default_opts = '--nogroup --nocolor --column'
let g:unite_source_grep_recursive_opt = ''

call unite#filters#matcher_default#use(['matcher_fuzzy'])

" Custom mappings for the unite buffer
autocmd FileType unite call s:unite_settings()
function! s:unite_settings()
  " Enable navigation with control-j and control-k in insert mode
  imap <buffer> <C-j>   <Plug>(unite_select_next_line)
  imap <buffer> <C-k>   <Plug>(unite_select_previous_line)
  " Reload directory contents
  imap <buffer> <C-r>   <Plug>(unite_redraw)
endfunction

" NeoComplete settings
let g:neocomplete#enable_at_startup = 1
let g:neocomplete#enable_smart_case = 1
let g:neocomplete#sources#syntax#min_keyword_length = 3

" NeoComplete Keybindings
inoremap <expr><C-g>     neocomplete#undo_completion()
inoremap <expr><C-l>     neocomplete#complete_common_string()

" Recommended key-mappings.
" <CR>: close popup and save indent.
inoremap <silent> <CR> <C-r>=<SID>my_cr_function()<CR>
function! s:my_cr_function()
  return neocomplete#smart_close_popup() . "\<CR>"
  " For no inserting <CR> key.
  "return pumvisible() ? neocomplete#close_popup() : "\<CR>"
endfunction
" <TAB>: completion.
inoremap <expr><TAB>  pumvisible() ? "\<C-n>" : "\<TAB>"
" <C-h>, <BS>: close popup and delete backword char.
inoremap <expr><C-h> neocomplete#smart_close_popup()."\<C-h>"
inoremap <expr><BS> neocomplete#smart_close_popup()."\<C-h>"
inoremap <expr><C-y>  neocomplete#close_popup()
inoremap <expr><C-e>  neocomplete#cancel_popup()

" NeoComplete omni completetion
autocmd FileType css setlocal omnifunc=csscomplete#CompleteCSS
autocmd FileType html,markdown setlocal omnifunc=htmlcomplete#CompleteTags
autocmd FileType javascript setlocal omnifunc=javascriptcomplete#CompleteJS
autocmd FileType python setlocal omnifunc=pythoncomplete#Complete
autocmd FileType xml setlocal omnifunc=xmlcomplete#CompleteTags

" }}}
"
" Autocommands {{{
" -----------------------------------------------------------------------------

" Indent rules
autocmd FileType markdown setlocal ts=4 sw=4 sts=4 tw=79
autocmd FileType python setlocal ts=4 sw=4 sts=4

" Folding rules
autocmd FileType c,cpp setlocal foldmethod=syntax

" }}}
"
" Functions {{{
" -----------------------------------------------------------------------------

function! ToggleFoldMethod()
if &foldmethod == 'indent'
  set foldmethod=marker
  echo "foldmethod=marker"
else
  set foldmethod=indent
  echo "foldmethod=indent"
endif
endfunction

" Toggle Vexplore
function! ToggleVExplorer()
  if exists("t:expl_buf_num")
      let expl_win_num = bufwinnr(t:expl_buf_num)
      if expl_win_num != -1
          let cur_win_nr = winnr()
          exec expl_win_num . 'wincmd w'
          close
          exec cur_win_nr . 'wincmd w'
          unlet t:expl_buf_num
      else
          unlet t:expl_buf_num
      endif
  else
      exec '1wincmd w'
      Vexplore
      let t:expl_buf_num = bufnr("%")
  endif
endfunction

" }}}
