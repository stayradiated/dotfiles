#!/usr/bin/env zsh

set -euo pipefail

# https://github.com/LedgerHQ/ledger-live-desktop/blob/master/src/main/updater/ledger-pubkey.js
# https://github.com/LedgerHQ/ledger-live/blob/develop/apps/ledger-live-desktop/src/main/updater/ledger-pubkey.js
public_key='-----BEGIN PUBLIC KEY-----
MFYwEAYHKoZIzj0CAQYFK4EEAAoDQgAEN7qcsG6bogi1nkD3jnMWS813wWguYEcI
CRcijSvFskSFjHB5la4xUt+Omb2t6iUwop+JRy+EUhy0UQ9p/cPsQA==
-----END PUBLIC KEY-----'

f_tmp=$(mktemp -d)
pushd $f_tmp

f_release=./ledger-live.txt
f_sha512sum=./ledger-live.sha512sum
f_sha512sum_sig=./ledger-live.sha512sum.sig
f_public_key=./ledgerlive.pem

echo "» Fetching releases from https://github.com/LedgerHQ/ledger-live/releases"

http https://api.github.com/repos/LedgerHQ/ledger-live/releases > $f_release

tag_name=$(< $f_release | jq -r '.[].tag_name' | grep '^@ledgerhq/live-desktop' | sort | tail -n 1)
version=$(echo "${tag_name}" | rg -o '[\d.]+')
download_url="https://download.live.ledger.com/ledger-live-desktop-${version}-linux-x86_64.AppImage"

echo "» Latest version is v${version}"

f_dest="$HOME/src/ledger-live-desktop-${version}"
if [ -d "${f_dest}" ]; then
  echo "» Ledger Live Desktop is up to date (${version})"
  exit 0
fi

echo "» Fetching signatures from https://www.ledger.com/ledger-live/lld-signatures"

http "https://resources.live.ledger.app/public_resources/signatures/ledger-live-desktop-${version}.sha512sum" > $f_sha512sum
http "https://resources.live.ledger.app/public_resources/signatures/ledger-live-desktop-${version}.sha512sum.sig" > $f_sha512sum_sig

echo $public_key > $f_public_key

echo "» Verifying signatures match public key"

openssl dgst -sha256 -verify $f_public_key -signature $f_sha512sum_sig $f_sha512sum

echo "» Downloading ledger-live-desktop binary"
wget "${download_url}"
f_binary=$(ls ./*.AppImage)

echo "» Verifying binary matches signature"
sha512sum --check --ignore-missing $f_sha512sum

echo "» Extracting files from AppImage"
chmod +x $f_binary
$f_binary --appimage-extract

echo "» Moving to ${f_dest}"
mv squashfs-root $f_dest

echo "» Updating ledger-live-desktop link"
ln -sf "${f_dest}/ledger-live-desktop" "${HOME}/dotfiles/bin/ledger-live-desktop"

echo "» Removing tempoary files"
popd
rm -rf $f_tmp

echo "» Successfully installed ledger-live-desktop ${version}"
