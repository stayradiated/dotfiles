#!/usr/bin/env zsh

#
# SLACK STATUS
# ============
#
# setup:
# create a Slack App with this config:
#
#     display_information:
#       name: Slack Status CLI
#     oauth_config:
#       scopes:
#         user:
#           - users.profile:write
#           - users.profile:read
#     settings:
#       org_deploy_enabled: false
#       socket_mode_enabled: false
#       token_rotation_enabled: false
#
# Get your OAuth Token
# "OAuth & Permissions" → "OAuth Tokens for Your Workspace" → "User OAuth Token"
#
# Save this in a file that you can import
#
#   export SLACK_TOKEN="xoxp-************-*************-*************-********************************"
#
# usage:
#
#   slack-status online
#   slack-status online 'Auckland, NZ'
#   slack-status away
#   slack-status offline

set -euo pipefail

source "${HOME}/dotfiles/apps/slack/token.enc"

function setStatus () {
  statusText=$1
  statusEmoji=$2
  statusExpiration=$3

  http \
    POST \
    'https://slack.com/api/users.profile.set' \
    "profile=={\"status_text\":\"${statusText}\",\"status_emoji\":\"${statusEmoji}\",\"status_expiration\":\"${statusExpiration}\"}" \
    "token==${SLACK_TOKEN}" \
  | jq -r '.profile.status_text'
}

function getStatus () {
  http \
    --print="b" \
    POST \
    'https://slack.com/api/users.profile.get' \
    "token==${SLACK_TOKEN}" \
  | jq -r '.profile.status_text'
}


function setStatusOnline () {
  emoji=":innocent:"
  if [[ -n "$1" ]]
  then
    text="Available, working from ${1} ($(date +'%D %T'))"
  else
    text="Available ($(date +'%D %T'))"
  fi
  expiration="$(( $(date +'%s') + (4 * 60 * 60) ))"
  setStatus $text $emoji $expiration
}

function setStatusAway () {
  emoji=":hear_no_evil:"
  text="Away from my laptop, will be back online later today ($(date +'%D %T'))"
  expiration="$(( $(date +'%s') + (4 * 60 * 60) ))"
  setStatus $text $emoji $expiration
}

function setStatusOffline () {
  emoji=":x:"
  text="Not available ($(date +'%D %T'))"
  expiration="0"
  setStatus $text $emoji $expiration
}

if [[ -v 1 ]]
then
  case $1 in 
    online)
      setStatusOnline ${2:-""}
      ;;
    away)
      setStatusAway
      ;;
    offline)
      setStatusOffline
      ;;
    *)
      getStatus
      ;;
  esac
else 
  getStatus
fi
